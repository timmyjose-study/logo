; LOGO's catch and throw are analogous to Common Lisp's catch and throw

to quiz 
  qa [Which is the oldest extant programming language today?] "Fortran
  qa [Which is the fastest programming language in the world?] "C
  qa [Which is the most beauriful algorithm in the world?] [Topological Sort]
end

to qa :question :answer
  catch "correct [ask.thrice :question :answer]
end

to ask.thrice :question :answer
  repeat 3 [ask.once :question :answer]
  print sentence [The correct answer is] :answer
end

to ask.once :question :answer
  local "attempt
  print :question
  make "attempt readlist
  if equalp count :attempt 1 ~
    [if equalp first :attempt :answer [print [Right!] throw "correct]]
  if equalp :attempt :answer [print [Right] throw "correct]
  print [Sorry, that's wrong.]
end