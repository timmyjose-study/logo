to quiz
  local "score 
  make "score 0
  make "score :score + ask.thrice [Which is the oldest extant programming language today?] "Fortran
  make "score :score + ask.thrice [Who is the fastest man alive?] [Usain Bolt]
  make "score :score + ask.thrice [What is the scientific name for a gorilla?] [Gorilla gorilla]
  print sentence [Your total score is] :score
end

to ask.thrice :question :answer
  repeat 3 [if ask.once :question :answer [output 1]]
  print sentence [The answer is] :answer
  output 0
end

to ask.once :question :answer
  local "attempt
  print :question
  make "attempt readlist
  if equalp count :attempt 1 ~ 
    [if equalp first :attempt :answer [print [Right!] output "true]]
  if equalp :attempt :answer [print [Right!] output "true]
  print [Sorry, that's wrong!]
  output "false
end